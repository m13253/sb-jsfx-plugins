/*
  Live Loudness Normalizer
  Copyright (c) 2020 Star Brilliant

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.
*/

desc:Live Loudness Normalizer [StarBrilliant]
slider1:target_lufs=-23<-60,0,0.1>Target Loudness (LUFS)
slider2:half_life_sec=3<0,10,0.1>Decay Half-life (sec)
slider3:limit_lu=13<0,24,0.1>Upper Hard Limit (LU)
slider4:inflect_lu=-6<-24,0,0.1>Lower Inflection Level (LU)
slider5:gate_lufs=-48<-60,0,0.1>Bottom Gate Level (LUFS)

in_pin:left input
in_pin:right input
in_pin:center input
in_pin:LFE input
in_pin:side left input
in_pin:side right input
in_pin:rear left input
in_pin:rear right input
out_pin:left output
out_pin:right output
out_pin:center output
out_pin:LFE output
out_pin:side left input
out_pin:side right input
out_pin:rear left input
out_pin:rear right input

@init

function init_ebur128_filter() local(
  f0, G, Q, K, Vh, Vb, a0,
  pb0, pb1, pb2, pa0, pa1, pa2,
  rb0, rb1, rb2, ra0, ra1, ra2
) (
  // https://github.com/jiixyj/libebur128/blob/master/ebur128/ebur128.c
  // MIT License

  f0 = 1681.974450955533;
  G = 3.999843853973347;
  Q = 0.7071752369554196;

  K = tan($pi * f0 / srate);
  Vh = pow(10, G / 20);
  Vb = pow(Vh, 0.4996667741545416);

  a0 = 1 + K/Q + sqr(K);
  pb0 = (Vh + Vb*K/Q + sqr(K)) / a0;
  pb1 = 2 * (K*K - Vh) / a0;
  pb2 = (Vh - Vb*K/Q + sqr(K)) / a0;
  pa0 = 1;
  pa1 = 2 * (sqr(K) - 1) / a0;
  pa2 = (1 - K/Q + sqr(K)) / a0;

  f0 = 38.13547087602444;
  Q = 0.5003270373238773;
  K = tan($pi * f0 / srate);

  rb0 = 1;
  rb1 = -2;
  rb2 = 1;
  ra0 = 1;
  ra1 = 2 * (sqr(K) - 1) / (1 + K/Q + sqr(K));
  ra2 = (1 - K/Q + sqr(K)) / (1 + K/Q + sqr(K));

  d.b0 = pb0*rb0;
  d.b1 = pb0*rb1 + pb1*rb0;
  d.b2 = pb0*rb2 + pb1*rb1 + pb2*rb0;
  d.b3 = pb1*rb2 + pb2*rb1;
  d.b4 = pb2*rb2;

  d.a0 = pa0*ra0;
  d.a1 = pa0*ra1 + pa1*ra0;
  d.a2 = pa0*ra2 + pa1*ra1 + pa2*ra0;
  d.a3 = pa1*ra2 + pa2*ra1;
  d.a4 = pa2*ra2;

  0;
);

function filter_signal() local(
  offset0, offset1, offset2, offset3, offset4, ch
) (
  offset0 = offset.unmixed * num_channels;
  offset1 = (offset.unmixed + (offset.unmixed < 1 ? filter_length) - 1) * num_channels;
  offset2 = (offset.unmixed + (offset.unmixed < 2 ? filter_length) - 2) * num_channels;
  offset3 = (offset.unmixed + (offset.unmixed < 3 ? filter_length) - 3) * num_channels;
  offset4 = (offset.unmixed + (offset.unmixed < 4 ? filter_length) - 4) * num_channels;
  array.unfiltered[offset0] = spl0;
  array.unfiltered[offset0 + 1] = spl1;
  array.unfiltered[offset0 + 2] = spl2;
  array.unfiltered[offset0 + 3] = spl3;
  array.unfiltered[offset0 + 4] = spl4;
  array.unfiltered[offset0 + 5] = spl5;
  array.unfiltered[offset0 + 6] = spl6;
  array.unfiltered[offset0 + 7] = spl7;
  ch = 0;
  loop(num_channels,
    array.filtered[offset0 + ch] = (
      array.unfiltered[offset0 + ch] * d.b0 +
      array.unfiltered[offset1 + ch] * d.b1 +
      array.unfiltered[offset2 + ch] * d.b2 +
      array.unfiltered[offset3 + ch] * d.b3 +
      array.unfiltered[offset4 + ch] * d.b4 -
      array.filtered[offset1 + ch] * d.a1 -
      array.filtered[offset2 + ch] * d.a2 -
      array.filtered[offset3 + ch] * d.a3 -
      array.filtered[offset4 + ch] * d.a4
    ) / d.a0;
    ch += 1;
  );
  array.mixed[offset.mixed] =
    sqr(array.filtered[offset0]) +            // Left
    sqr(array.filtered[offset0 + 1]) +        // Right
    sqr(array.filtered[offset0 + 2]) +        // Center
    // 0 +                                    // LFE
    sqr(array.filtered[offset0 + 4]) * 1.41 + // Side Left
    sqr(array.filtered[offset0 + 5]) * 1.41 + // Side Right
    sqr(array.filtered[offset0 + 6]) * 1.41 + // Rear Left
    sqr(array.filtered[offset0 + 7]) * 1.41;  // Rear Right
);

function measure_momentary_loudness_lufs() local(
  i, sum
) (
  i = 0;
  sum = 0;
  loop(buffer_length,
    sum += array.mixed[i];
    i += 1;
  );
  log10(sum / max(i, 1)) * 10 - 0.691;
);

ext_nodenorm = 1;
gfx_ext_retina = 1;
num_channels = 8;
momentary_lufs = target_lufs;
gain_db = 0;
filter_length = 5;
// Buffer 400ms for momentary loudness measurement
buffer_length = max(floor(srate * 0.4 + 0.5), 1);
// Measure every 5ms
// (Humans need a minimum of about 0.01 to 0.02 second of auditory experience to perceive duration)
measure_interval = max(floor(srate * 0.005 + 0.5), 1);
array.unfiltered = 0;
array.filtered = filter_length * num_channels;
array.mixed = array.filtered + filter_length * num_channels;
array.length = array.mixed + buffer_length;
offset.unmixed = 0;
offset.mixed = 0;
offset.measure = 0;
memset(0, 0, array.length);
freembuf(array.length + 1);
init_ebur128_filter();

@sample

filter_signal();
offset.unmixed += 1;
offset.unmixed >= filter_length ? offset.unmixed = 0;
offset.mixed += 1;
offset.mixed >= buffer_length ? offset.mixed = 0;
offset.measure += 1;
offset.measure >= measure_interval ? (
  offset.measure = 0;
  momentary_lufs = measure_momentary_loudness_lufs();
);
diff_lu = (momentary_lufs > gate_lufs ? target_lufs - momentary_lufs) - gain_db;
gain_db += diff_lu * min(
  (diff_lu > 0 && inflect_lu < -0.05 ?
    sqr(inflect_lu) / (sqr(inflect_lu) + sqr(diff_lu)) :
    1
  ) * (log(2) / (half_life_sec * srate)),
  1 / (measure_interval - offset.measure)
);
gain_db = min(max(gain_db, gate_lufs), target_lufs + limit_lu - momentary_lufs);

gain = pow(10, gain_db * 0.05);
spl0 *= gain;
spl1 *= gain;
spl2 *= gain;
spl3 *= gain;
spl4 *= gain;
spl5 *= gain;
spl6 *= gain;
spl7 *= gain;

@gfx 520 64

function gfx_rect_aa(x y x2 y2) local(
  left, left_bbox, right, right_bbox,
  top, top_bbox, bottom, bottom_bbox,
  fg_r, fg_g, fg_b, fg_a, bg_r, bg_g, bg_b
) (
  left = x;
  right = x2;
  top = y;
  bottom = y2;
  left_bbox = max(floor(left), 0);
  right_bbox = min(ceil(right), gfx_w);
  top_bbox = max(floor(top), 0);
  bottom_bbox = min(ceil(bottom), gfx_h);
  fg_r = max(gfx_r - 1/510, 0);
  fg_g = max(gfx_g - 1/510, 0);
  fg_b = max(gfx_b - 1/510, 0);
  fg_r = fg_r <= 0.040449936 ? fg_r / 12.92 : pow((fg_r+0.055) / 1.055, 2.4);
  fg_g = fg_g <= 0.040449936 ? fg_g / 12.92 : pow((fg_g+0.055) / 1.055, 2.4);
  fg_b = fg_b <= 0.040449936 ? fg_b / 12.92 : pow((fg_b+0.055) / 1.055, 2.4);

  gfx_y = top_bbox;
  loop(bottom_bbox - top_bbox,
    gfx_x = left_bbox;
    loop(right_bbox - left_bbox,
      fg_a = gfx_a * (min(gfx_x + 1, right) - max(gfx_x, left)) * (min(gfx_y + 1, bottom) - max(gfx_y, top));
      gfx_getpixel(bg_r, bg_g, bg_b);
      bg_r = bg_r <= 0.040449936 ? bg_r / 12.92 : pow((bg_r+0.055) / 1.055, 2.4);
      bg_g = bg_g <= 0.040449936 ? bg_g / 12.92 : pow((bg_g+0.055) / 1.055, 2.4);
      bg_b = bg_b <= 0.040449936 ? bg_b / 12.92 : pow((bg_b+0.055) / 1.055, 2.4);
      bg_r = bg_r * (1 - fg_a) + fg_r * fg_a;
      bg_g = bg_g * (1 - fg_a) + fg_g * fg_a;
      bg_b = bg_b * (1 - fg_a) + fg_b * fg_a;
      bg_r = bg_r <= 0.0031308 ? bg_r * 12.92 : 1.055 * pow(bg_r, 1 / 2.4) - 0.055;
      bg_g = bg_g <= 0.0031308 ? bg_g * 12.92 : 1.055 * pow(bg_g, 1 / 2.4) - 0.055;
      bg_b = bg_b <= 0.0031308 ? bg_b * 12.92 : 1.055 * pow(bg_b, 1 / 2.4) - 0.055;
      bg_r = min(bg_r + 1/510, 1);
      bg_g = min(bg_g + 1/510, 1);
      bg_b = min(bg_b + 1/510, 1);
      gfx_setpixel(bg_r, bg_g, bg_b);
      gfx_x += 1;
    );
    gfx_y += 1;
  );
);

function gfx_shadow_aa(x y x2 y2 z outline) local(
  left, left_bbox, right, right_bbox,
  top, top_bbox, bottom, bottom_bbox,
  bg_r, bg_g, bg_b, bg_a,
) (
  left = x - outline;
  right = x2 + outline;
  top = y - outline;
  bottom = y2 + outline;
  left_bbox = max(floor(left - 8*z), 0);
  right_bbox = min(ceil(right + 8*z), gfx_w);
  top_bbox = max(floor(top - 8*z), 0);
  bottom_bbox = min(ceil(bottom + 8*z), gfx_h);

  gfx_y = top_bbox;
  loop(bottom_bbox - top_bbox,
    gfx_x = left_bbox;
    loop(right_bbox - left_bbox,
      // Pre-integrated Lambert diffusion under hemisphere illumination
      // Midpoint rule is used because it does not require heavy computation
      bg_a = 1 - min(max(gfx_a * (
        atan((gfx_x + 0.5 - left) * (gfx_y + 0.5 - top) * invsqrt(sqr(gfx_x + 0.5 - left) + sqr(gfx_y + 0.5 - top) + sqr(z))) -
        atan((gfx_x + 0.5 - right) * (gfx_y + 0.5 - top) * invsqrt(sqr(gfx_x + 0.5 - right) + sqr(gfx_y + 0.5 - top) + sqr(z))) -
        atan((gfx_x + 0.5 - left) * (gfx_y + 0.5 - bottom) * invsqrt(sqr(gfx_x + 0.5 - left) + sqr(gfx_y + 0.5 - bottom) + sqr(z))) +
        atan((gfx_x + 0.5 - right) * (gfx_y + 0.5 - bottom) * invsqrt(sqr(gfx_x + 0.5 - right) + sqr(gfx_y + 0.5 - bottom) + sqr(z)))
      ) / (2 * $pi), 0), 1);
      gfx_getpixel(bg_r, bg_g, bg_b);
      bg_r = bg_r <= 0.040449936 ? bg_r / 12.92 : pow((bg_r+0.055) / 1.055, 2.4);
      bg_g = bg_g <= 0.040449936 ? bg_g / 12.92 : pow((bg_g+0.055) / 1.055, 2.4);
      bg_b = bg_b <= 0.040449936 ? bg_b / 12.92 : pow((bg_b+0.055) / 1.055, 2.4);
      bg_r *= bg_a;
      bg_g *= bg_a;
      bg_b *= bg_a;
      bg_r = bg_r <= 0.0031308 ? bg_r * 12.92 : 1.055 * pow(bg_r, 1 / 2.4) - 0.055;
      bg_g = bg_g <= 0.0031308 ? bg_g * 12.92 : 1.055 * pow(bg_g, 1 / 2.4) - 0.055;
      bg_b = bg_b <= 0.0031308 ? bg_b * 12.92 : 1.055 * pow(bg_b, 1 / 2.4) - 0.055;
      bg_r = min(bg_r + 1/510, 1);
      bg_g = min(bg_g + 1/510, 1);
      bg_b = min(bg_b + 1/510, 1);
      gfx_setpixel(bg_r, bg_g, bg_b);
      gfx_x += 1;
    );
    gfx_y += 1;
  );
);

function db_to_pixel(db) (
  gfx_w * (db + 37.5) / 75;
);

gfx_clear = 0;
// Force the clear operation to complete,
// prevents gfx_getpixel from misbehaving.
gfx_setpixel(0, 0, 0);

gfx_set(0.943);
gfx_setfont(2, "MS Shell Dlg 2", 11 * gfx_ext_retina);

gui_mark_db = 0;
gui_mark_skip = 25 * 18*gfx_ext_retina / gfx_w;
loop(12,
  gui_mark_db -= 3;
  gui_mark_skip -= 1;
  gui_mark_skip <= 0 ? (
    gui_mark_skip = 25 * 18*gfx_ext_retina / gfx_w;
    gui_mark_px = db_to_pixel(gui_mark_db);
    gfx_rect_aa(gui_mark_px - 0.625*gfx_ext_retina, gfx_h/2 - 12*gfx_ext_retina, gui_mark_px + 0.625*gfx_ext_retina, gfx_h/2 - 6*gfx_ext_retina);
    gfx_rect_aa(gui_mark_px - 0.625*gfx_ext_retina, gfx_h/2 + 10*gfx_ext_retina, gui_mark_px + 0.625*gfx_ext_retina, gfx_h/2 + 16*gfx_ext_retina);
    gfx_x = floor(gui_mark_px + 0.5);
    gfx_y = floor(gfx_h/2 + 16*gfx_ext_retina + 0.5);
    sprintf(0, "%+d", gui_mark_db);
    gfx_drawstr(0, $x101, gfx_x, gfx_y);
  );
);

gui_mark_px = db_to_pixel(0);
gfx_rect_aa(gui_mark_px - 0.625*gfx_ext_retina, gfx_h/2 - 12*gfx_ext_retina, gui_mark_px + 0.625*gfx_ext_retina, ceil(gfx_h/2 - 8*gfx_ext_retina));
gfx_rect_aa(gui_mark_px - 0.625*gfx_ext_retina, floor(gfx_h/2 + 12*gfx_ext_retina), gui_mark_px + 0.625*gfx_ext_retina, gfx_h/2 + 16*gfx_ext_retina);
gfx_x = floor(gui_mark_px + 0.5);
gfx_y = floor(gfx_h/2 + 16*gfx_ext_retina + 0.5);
gfx_drawstr("0", $x101, gfx_x, gfx_y);

gui_mark_db = 0;
gui_mark_skip = 25 * 18*gfx_ext_retina / gfx_w;
loop(12,
  gui_mark_db += 3;
  gui_mark_skip -= 1;
  gui_mark_skip <= 0 ? (
    gui_mark_skip = 25 * 18*gfx_ext_retina / gfx_w;
    gui_mark_px = db_to_pixel(gui_mark_db);
    gfx_rect_aa(gui_mark_px - 0.625*gfx_ext_retina, gfx_h/2 - 12*gfx_ext_retina, gui_mark_px + 0.625*gfx_ext_retina, gfx_h/2 - 6*gfx_ext_retina);
    gfx_rect_aa(gui_mark_px - 0.625*gfx_ext_retina, gfx_h/2 + 10*gfx_ext_retina, gui_mark_px + 0.625*gfx_ext_retina, gfx_h/2 + 16*gfx_ext_retina);
    gfx_x = floor(gui_mark_px + 0.5);
    gfx_y = floor(gfx_h/2 + 16*gfx_ext_retina + 0.5);
    sprintf(0, "%+d", gui_mark_db);
    gfx_drawstr(0, $x101, gfx_x, gfx_y);
  );
);

gui_gain_px = db_to_pixel(gain_db);
gfx_shadow_aa(min(gui_gain_px, gfx_w/2 - 0.625*gfx_ext_retina), gfx_h/2 - 8*gfx_ext_retina, max(gui_gain_px, gfx_w/2 + 0.625*gfx_ext_retina), gfx_h/2 + 12*gfx_ext_retina, 2.5*gfx_ext_retina, 0.625*gfx_ext_retina);
gfx_set(0.167, 0.661, 0.457);
gfx_rect_aa(min(gui_gain_px, gfx_w/2 - 0.625*gfx_ext_retina), gfx_h/2 - 8*gfx_ext_retina, max(gui_gain_px, gfx_w/2 + 0.625*gfx_ext_retina), gfx_h/2 + 12*gfx_ext_retina);

gui_gate_px = db_to_pixel(gate_lufs - target_lufs + gain_db);
gui_diff_px = db_to_pixel(momentary_lufs - target_lufs + gain_db);
gfx_shadow_aa(0, gfx_h/2 - 3*gfx_ext_retina, gui_diff_px, gfx_h/2 + 7*gfx_ext_retina, 2.5*gfx_ext_retina, 0.625*gfx_ext_retina);
gfx_set(0.873);
gfx_rect_aa(0, gfx_h/2 - 3*gfx_ext_retina, min(ceil(gui_gate_px), gui_diff_px), gfx_h/2 + 7*gfx_ext_retina);
gfx_set(0, 0.998, 0.586);
gfx_rect_aa(gui_gate_px, gfx_h/2 - 3*gfx_ext_retina, gui_diff_px, gfx_h/2 + 7*gfx_ext_retina);

gui_limit_px = db_to_pixel(limit_lu);
gfx_shadow_aa(gui_limit_px - 0.625*gfx_ext_retina, gfx_h/2 - 6*gfx_ext_retina, gui_limit_px + 0.625*gfx_ext_retina, gfx_h/2 + 10*gfx_ext_retina, 2.5*gfx_ext_retina, 0.625*gfx_ext_retina);
gfx_set(0.998, 0.253, 0);
gfx_rect_aa(gui_limit_px - 0.625*gfx_ext_retina, gfx_h/2 - 6*gfx_ext_retina, gui_limit_px + 0.625*gfx_ext_retina, gfx_h/2 + 10*gfx_ext_retina);

gfx_set(0.943);
gfx_setfont(1, "MS Shell Dlg", 12 * gfx_ext_retina);
gfx_x = floor(4*gfx_ext_retina + 0.5);
gfx_y = floor(min(gfx_h/2 - 16*gfx_ext_retina, 16*gfx_ext_retina) + 0.5);
sprintf(0, "Gain: %+.1f dB", gain_db);
gfx_drawstr(0, $x108, gfx_x, gfx_y);
